MEMORY
{
    LR_FLASH : ORIGIN = 0x00160000, LENGTH = 0xC0000
    LR_CONFIG : ORIGIN = 0x01FE0000, LENGTH = 0x00020000
    LR_DAT : ORIGIN = 0x00220000, LENGTH = 0x20000
    HEAP : ORIGIN = 0xA0000000, LENGTH = 0x1FFFFFC + 1
    STACK : ORIGIN = 0x5C03A000, LENGTH = 0x2000 + 1
    RELOC_RAM : ORIGIN = 0x00000000, LENGTH = 1M
}
ENTRY(EntryPoint)
SECTIONS
{
    ER_FLASH  :
    {
        * (i.EntryPoint)
        * (SectionForBootstrapOperations)
        * (.text*)
        * (i.*)
        * (t.*)
        * (.rodata*)
        * (rodata)
        * (.constdata*)
        * (.conststring*)
        PROVIDE(_sbrk = .);
        PROVIDE(_write = .);
        PROVIDE(_close = .);
        PROVIDE(_fstat = .);
        PROVIDE(_lseek = .);
        PROVIDE(_read = .);
        PROVIDE(_exit = .);
        PROVIDE(_getpid = .);
        PROVIDE(_kill = .);
        PROVIDE(abort = .);
        PROVIDE(__errno = .);
        PROVIDE(_read = .);
        PROVIDE(isatty = .);
        PROVIDE(_isatty = .);
        LONG(0xE12FFF1E); 
    }>LR_FLASH
    ER_RAM_RO  : ALIGN(0x10)
    {
        * (VectorsTrampolines)
        * (SectionForFlashOperations)
    }>RELOC_RAM AT>LR_FLASH
    ER_RAM_RW  : ALIGN(0x10)
    {
        * (rwdata)
        * (.data*)
    }>RELOC_RAM AT>LR_FLASH
    .bss  : ALIGN(0x10)
    {
        * (.zidata*)
        * (.bss*)
        PROVIDE(__exidx_start = .); 
        PROVIDE(__exidx_end = .); 
    }>RELOC_RAM
    /DISCARD/  :
    {
        * (.glue*)
        * (.vfp11*)
        * (.ARM.exidx*)
        * (.ARM.extab*)
    }
    ER_HEAP_BEGIN 0xA0000000 :
    {
        * (SectionForHeapBegin)
    }
    ER_HEAP_END 0xA1FFFFFC :
    {
        * (SectionForHeapEnd)
    }
    ER_STACK_BOTTOM 0x5C03A000 :
    {
        * (SectionForStackBottom)
    }
    ER_STACK_TOP 0x5C03C000 :
    {
        * (SectionForStackTop)
    }
    ER_CONFIG  :
    {
        * (SectionForConfig)
    }>LR_CONFIG
    ER_DAT  :
    {
        * (tinyclr_metadata)
    }>LR_DAT
}
Load$$ER_FLASH$$Base = ADDR(ER_FLASH);
Image$$ER_FLASH$$Length = SIZEOF(ER_FLASH);
Image$$ER_RAM_RO$$Base = ADDR(ER_RAM_RO);
Image$$ER_RAM_RO$$Length = SIZEOF(ER_RAM_RO);
Load$$ER_RAM_RO$$Base = LOADADDR(ER_RAM_RO);
Image$$ER_RAM_RW$$Base = ADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$Length = SIZEOF(ER_RAM_RW);
Load$$ER_RAM_RW$$Base = LOADADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$ZI$$Base = ADDR(.bss);
Image$$ER_RAM_RW$$ZI$$Length = SIZEOF(.bss);
__use_no_semihosting_swi = 0;
